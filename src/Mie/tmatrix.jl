@doc raw"""
According to Eq. (5.42) -- Eq. (5.44) in Mishchenko et al. (2002), the T-Matrix for a Mie particle can be written as:

```math
\begin{array}{l}
T_{m n m^{\prime} n^{\prime}}^{12}(P) \equiv 0, \quad T_{m n m^{\prime} n^{\prime}}^{21}(P) \equiv 0, \\
T_{m n m^{\prime} n^{\prime}}^{11}(P)=-\delta_{m m^{\prime}} \delta_{n n^{\prime}} b_n, \\
T_{m n m^{\prime} n^{\prime}}^{22}(P)=-\delta_{m m^{\prime}} \delta_{n n^{\prime}} a_n .
\end{array}
```

`MieTransitionMatrix{CT, N}(x::Real, m::Number)` 

Generate the T-Matrix from the Mie coefficients of a homogeneous sphere.

`MieTransitionMatrix{CT, N}(x_core::Real, x_mantle::Real, m_core::Number, m_mantle::Number)`

Generate the T-Matrix from the Mie coefficients of a coated sphere.

This struct provides the T-Matrix API for a Mie particle.
"""
struct MieTransitionMatrix{CT, N, V <: AbstractVector{CT}} <:
       AbstractTransitionMatrix{CT, N}
    a::V
    b::V
end

function MieTransitionMatrix{CT, N}(x::Real, m::Number) where {CT, N}
    T = real(CT)
    a, b = bhmie(T, x, m; nâ‚˜â‚â‚“ = N)
    MieTransitionMatrix{CT, N, Vector{CT}}(a, b)
end

function MieTransitionMatrix{CT, N}(x_core::Real, x_mantle::Real, m_core::Number,
                                    m_mantle::Number) where {CT, N}
    T = real(CT)
    a, b = bhcoat(T, x_core, x_mantle, m_core, m_mantle; nâ‚˜â‚â‚“ = N)
    MieTransitionMatrix{CT, N, Vector{CT}}(a, b)
end

function Base.copy(ğ“::MieTransitionMatrix{CT, N}) where {CT, N}
    MieTransitionMatrix{CT, N, Vector{CT}}(copy(ğ“.a), copy(ğ“.b))
end
Base.@propagate_inbounds function Base.getindex(mie::MieTransitionMatrix{CT, N, V},
                                                m::Integer, n::Integer, mâ€²::Integer,
                                                nâ€²::Integer, p::Integer,
                                                pâ€²::Integer) where {CT, N, V}
    if m != mâ€² || n != nâ€² || p != pâ€² || abs(m) > n
        zero(CT)
    else
        p == 1 ? -mie.b[n] : -mie.a[n]
    end
end

rotate(ğ“::MieTransitionMatrix, ::Rotation{3}) = copy(ğ“)

@testitem "MieTransitionMatrix" begin
    using TransitionMatrices: MieTransitionMatrix, RotZYZ, TransitionMatrix, rotate

    @testset "of a homogeneous sphere remains the same under rotations" begin
        ğ“ = MieTransitionMatrix{ComplexF64, 5}(1.0, 1.311)
        @test all(isapprox.(ğ“,
                            rotate(TransitionMatrix{ComplexF64, 5, typeof(ğ“)}(ğ“),
                                   RotZYZ(0.2, 0.3, 0.4)); atol = eps(Float64)))
        @test all(isapprox.(ğ“,
                            rotate(TransitionMatrix{ComplexF64, 5, typeof(ğ“)}(ğ“),
                                   RotZYZ(0.8, 0.0, -1.0)); atol = eps(Float64)))
    end

    @testset "of a coated sphere remains the same under rotations" begin
        ğ“ = MieTransitionMatrix{ComplexF64, 5}(0.4, 0.8, 1.0, 1.311)
        @test all(isapprox.(ğ“,
                            rotate(TransitionMatrix{ComplexF64, 5, typeof(ğ“)}(ğ“),
                                   RotZYZ(0.2, 0.3, 0.4)); atol = eps(Float64)))
        @test all(isapprox.(ğ“,
                            rotate(TransitionMatrix{ComplexF64, 5, typeof(ğ“)}(ğ“),
                                   RotZYZ(0.8, 0.0, -1.0)); atol = eps(Float64)))
    end
end
